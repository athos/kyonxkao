Play = Title CharacterDeclarationList Act+
Title = <'「'> String <'」'>
<String> = #"[^。！？」]+"
Comment = #"[^。！？」]+"
CharacterDeclarationList = CharacterDeclaration+
<CharacterDeclaration> = Character <'：'> <Comment> <EndSymbol>
Act = ActHeader Scene+
<ActHeader> = ActNum <'：'> <'「'> <Comment> <'」'>
<ActNum> = <'第'> #"[0-9]+" <'幕'>
Scene = SceneHeader SceneContents
<SceneHeader> = SceneNum <'：'> <'「'> <Comment> <'」'>
<SceneNum> = <'第'> #"[0-9]+" <'場'>
<SceneContents> = SceneContent+
<SceneContent> = EnterExit | Line
<EnterExit> = Enter | Exit | Exeunt
Enter = <'['> CharacterList <'登場'> <']'>
Exit = <'['> CharacterList <'退場'> <']'>
Exeunt = <'['> <'全員退場'> <']'>
<CharacterList> = (Character <'、'>)* Character
<Character> = {{CHARACTERS}}
Line = Character <'：'> Sentence+
Sentence = UnconditionalSentence
         | Conditional <'、'> UnconditionalSentence
EndSymbol = QuestionSymbol | StatementSymbol

Value = CharacterRef
      | ConstantRef
      | Pronoun
      | Value <'と'> Value BinaryOperator
CharacterRef = Character
ConstantRef = Constant
Pronoun = FirstPerson | SecondPerson
FirstPerson = <'私'> | <'僕'> | <'我'> | <'俺'> | <'吾輩'>
SecondPerson = <'あなた'> | <'君'> | <'そなた'> | <'汝'>
<Constant> = UnarticulatedConstant
         | <FirstPerson> <'の'> UnarticulatedConstant
         | <SecondPerson> <'の'> UnarticulatedConstant
<UnarticulatedConstant> = PositiveConstant | NegativeConstant
PositiveConstant = (PositiveAdjective | NeutralAdjective)* PositiveNoun
PositiveNoun = <'女'>
PositiveAdjective = <'いい'>
NeutralAdjective = <'ふつうの'>
NegativeConstant = (NegativeAdjective | NeutralAdjective)* NegativeNoun
NegativeNoun = <'男'>
NegativeAdjective = <'悪い'>
Adjective = PositiveAdjective
          | NegativeAdjective
          | NeutralAdjective
BinaryOperator = SumOf
               | DifferenceOf
               | ProductOf
               | QuotientOf
               | RemainderOf
SumOf = <'の和'>
DifferenceOf = <'の差'>
ProductOf = <'の積'>
QuotientOf = <'の商'>
RemainderOf =  <'の剰余'>

Conditional = IfSo | IfNot
IfSo = <'もしそうなら'>
IfNot = <'もしそうでないなら'>
<UnconditionalSentence> = InOut
                        | Jump
                        | Question
                        | Recall
                        | Remember
                        | Statement
StatementSymbol = '。' | '！'
InOut = SpeakYourMind | OpenYourMind
SpeakYourMind = <SecondPerson> <'の気持ちを聞かせて下さい'> <StatementSymbol>
OpenYourMind = <SecondPerson> <'の心を開いて下さい'> <StatementSymbol>
Jump = ActNum <JumpPhrase> <StatementSymbol>
     | SceneNum <JumpPhrase> <StatementSymbol>
JumpPhrase = 'へ進みましょう'
           | 'へ進まなければなりません'
           | 'へ戻りましょう'
           | 'へ戻らなければなりません'
Question = Value <'は'> Value Comparison <QuestionSymbol>
QuestionSymbol = '？'
Recall = <String> <'を思い出して下さい'> <StatementSymbol>
Remember = Value <'を覚えて下さい'> <StatementSymbol>
Statement = <SecondPerson> <'は'> Constant <'です'> <StatementSymbol>
          | <SecondPerson> <'は'> Value Equality <'です'> <StatementSymbol>
Comparison = NonnegatedComparison 'でない'
           | NonnegatedComparison
NonnegatedComparison = Equality | Inequality
Equality = <'と同じくらい'> Adjective
Inequality = <'よりも'> Adjective
